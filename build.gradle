plugins {
  id 'doki-theme-plugin'
  id 'org.jetbrains.intellij' version '0.4.16'
  id 'org.jetbrains.kotlin.jvm' version '1.3.60-eap-143'
  id "org.jlleitschuh.gradle.ktlint" version "8.2.0"
  id 'org.kordamp.gradle.markdown' version '2.2.0'
}

group 'io.unthrottled'
version '10.2.1'

repositories {
  maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
  mavenCentral()
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation 'commons-io:commons-io:2.6'
  implementation 'org.javassist:javassist:3.21.0-GA'
  implementation 'io.sentry:sentry:1.7.30'
  testImplementation "org.assertj:assertj-core:3.8.0"
  testImplementation "io.mockk:mockk:1.10.0"
}

configurations {
  implementation.exclude group: 'org.slf4j'
}

intellij {
  version '2020.2'
  type 'IU'
  alternativeIdePath idePath

  if(System.getenv().getOrDefault("ENV", "") == "DEV") {
    plugins (
      'Key Promoter X:2020.2',
      'org.jetbrains.jumpToLine:0.1.1',
      'zd.zero.waifu-motivator-plugin:1.2'
    )
  }
}
compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

markdownToHtml {
  sourceDir file("$projectDir/changelog")
  outputDir file("$projectDir/build/html")
}

patchPluginXml {
  def releaseNotes ="$projectDir/build/html/RELEASE-NOTES.html"
  if(file(releaseNotes).exists()){
    changeNotes file(releaseNotes).text
  }
  sinceBuild sinceBuildVersion
  untilBuild untilBuildVersion
}
tasks.patchPluginXml.dependsOn("markdownToHtml")
tasks.patchPluginXml.dependsOn("buildThemes")
